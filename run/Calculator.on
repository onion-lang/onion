import {
	javax.swing.*
	java.awt.*
	java.awt.event.*
}

class Calculator : JFrame <: ActionListener {
	text			:JTextField
	left			:Long
	right		    :Long
	operator	    :String
	isError	    	:Boolean
public:
	def setValue(value : Long) { this.text.setText(JLong::toString(value)); }
	def setValue(value : String) { this.text.setText(value); }
	def actionPerformed(event : ActionEvent) {
		label = event.source$JButton.label
		try{
			if this.isError && !(label == "C") {
				return
			}
			input = JLong::parseLong(label)
			if this.operator != null {
				this.right = this.right * 10 + input
				setValue(this.right)
			}else{
				this.left = this.left * 10 + input
				setValue(this.left)
			}
		}catch e :NumberFormatException{
			if label == "C" {
				this.left = 0
				this.right = 0
				this.operator	= null
				this.text.setText(JLong::toString(0))
				this.isError = false
			}else{
				if this.operator != null {
					if this.operator == "/" {
						if this.right == 0L{
							setValue("0で割ることはできません。")
							this.left = 0L
							this.right = 0L
							this.isError = true
						}else{
							this.left = this.left / this.right
						}
						setValue(this.left)
					}else{
						select this.operator {
						case "+": this.left = this.left + this.right
						case "-": this.left = this.left - this.right
						case "*": this.left = this.left * this.right
						case "/":  this.left = this.left / this.right
						}
						setValue(this.left)
					}
				}
				this.operator = label
				this.right = 0
			}
		}
	}

	def this:("簡易電卓") {
		setDefaultCloseOperation(JFrame::EXIT_ON_CLOSE)
		setSize(800, 600)
		this.text		= new JTextField
		this.text.setHorizontalAlignment(JTextField::RIGHT)
		pane		= getContentPane()
		north		= new JPanel
		north.setLayout(new BorderLayout)
		north.add(this.text, BorderLayout::NORTH)
		center= new JPanel
		center.setLayout(new GridLayout(4, 5, 4, 3))
		center.setFont(new Font(null, Font::PLAIN, 8))
		labels = [
			"7", "8", "9", "C",
			"4", "5", "6", "*",
			"1", "2", "3", "-",
			"0", "=", "/", "+"
		]
		foreach label:String in labels {
			button = new JButton(label$String)
			button.addActionListener(this)
			button.setPreferredSize(new Dimension(42, 28))
			center.add(button)
		}
		pane.add(north, BorderLayout::NORTH)
		pane.add(center, BorderLayout::CENTER)
	}

	static def main(args :String[]){
		frame = new Calculator
		frame.pack
		frame.setVisible(true)
	}
}
