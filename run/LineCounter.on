import {
  java.io.File;
  java.io.BufferedReader;
  java.io.FileReader;
}

class LineCounter {
public:
  static def countLines(file: File): Int {
    val reader: BufferedReader = new BufferedReader(new FileReader(file));
    var count: Int = 0;
    for var line: String = null; (line = reader.readLine) != null; {
      count++;
    }
    return count;
  }

  static def process(file: File, callback: (File) -> Object): void {
    if(file == null) { return; }
    foreach myFile: File in file.listFiles {
      if myFile.isDirectory {
        process(myFile, callback);
      } else {
        callback.call(myFile);
      }
    }
  }

  static def main(args: String[]): void {
    val linesArray: Int[] = new Int[1];
    linesArray[0] = 0;

    val fileCounter: (File) -> Object = (f: File) -> {
      if f.name.endsWith(".scala") || f.name.endsWith(".java") || f.name.endsWith(".jj") {
        linesArray[0] = linesArray[0] + countLines(f);
      }
      return null;
    };

    foreach file: File in [new File("src"), new File("run")] {
      process(file, fileCounter);
    }
    IO::println("lines: " + linesArray[0]);
  }
}
